plugins {
    id 'maven-publish'
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'me.tylerbwong.gradle.metalava'
    id 'org.jetbrains.kotlinx.kover'
}

android {
    namespace 'com.example.testlibrary'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$compose_compiler_version"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

metalava {
    filename.set("api/$name-api.txt")
    sourcePaths.setFrom("src/main")
    keepFilename.set("api/$name-api.pro")
}

publishing {
    repositories {
        maven {
            name = "VersionCatalogPlugin"
            url = uri("https://maven.pkg.github.com/vlohachov/LokaliseAutomation")
            credentials {
                username = System.getenv("GITHUB_USER")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'com.example'
            artifactId = 'testlibrary'
            version = System.getenv("VERSION") ?: '0.0.1'

            afterEvaluate {
                from(components["release"])
            }
        }
    }
}

koverAndroid {
    report("debug") {
        filters {
            excludes {
                classes(
                        "*Fragment",
                        "*Fragment\$*",
                        "*Activity",
                        "*Activity\$*",
                        "*.databinding.*",
                        "*.BuildConfig",
                        "*ComposableSingletons\$*",
                        "*ColorKt*",
                        "*ThemeKt*",
                        "*TypeKt*",
                )
                annotatedBy("*Composable*")
            }
        }

        verify {
            rule {
                entity = kotlinx.kover.gradle.plugin.dsl.GroupingEntityType.CLASS

                minBound(70)
            }
        }
    }
}

dependencies {
    implementation project(":feature-module:kotlin-lib")

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.0'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.compose.material3:material3:1.1.0-alpha08'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation 'app.cash.turbine:turbine:0.12.1'
    testImplementation 'io.mockk:mockk:1.13.4'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}
